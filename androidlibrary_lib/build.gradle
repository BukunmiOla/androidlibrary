/**************************************************************************************************
 GRADLE CONFIG
 *************************************************************************************************/

if ((new File(gradle.local)).exists()) {
    apply from: gradle.local
} else {
    apply from: gradle.remote
}

libraryScripts.each {
    apply from: it
}

apply plugin: 'com.android.library'
apply plugin: "maven-publish"


android {
    namespace(groupId + libraryPackageName)
    compileSdk(compileVersion)
    defaultConfig {
        minSdkVersion(minVersion)
        targetSdkVersion(targetVersion)
        versionCode(releaseVersionCode)
        versionName(versionCodeName)
        testInstrumentationRunner(instrumentationRunner)
    }

    buildFeatures {
        aidl true
        buildConfig true
    }

    flavorDimensions.add("stage")

    productFlavors {
        demo {
            dimension 'stage'
        }

        master {
            dimension 'stage'
        }

        snapshot {
            dimension 'stage'
        }

    }

    buildTypes {
        release {
            minifyEnabled(minify)
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            matchingFallbacks = ['debug']
        }
        debug {
            debuggable(true)
            matchingFallbacks = ['release']
        }
    }

    android{
        packagingOptions {
            exclude 'META-INF/*'
        }
    }

    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }
}


configurations.all {
    resolutionStrategy {
        cacheDynamicVersionsFor(0, 'seconds')
        cacheChangingModulesFor(0, 'seconds')
    }
}

allprojects {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:deprecation"
    }
}


dependencies {
    repositories {
        maven {
            name = "GithubPackages"
            url = "https://maven.pkg.github.com/odk-x/sync-endpoint"
            credentials {
                username = githubMvnUser
                password = githubMvnPass
            }
        }
    }
    implementation 'commons-io:commons-io:2.8.0'
    implementation 'joda-time:joda-time:2.10.10'
    implementation 'org.opendatakit:odkx-rest-interface:2.1.10-SNAPSHOT'
    implementation 'net.jcip:jcip-annotations:1.0'
    implementation 'androidx.annotation:annotation:1.3.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.fragment:fragment:1.3.6'
    implementation 'com.google.android.material:material:1.5.0-alpha02'

    // Testing dependencies
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.annotation:annotation:1.7.1'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
}

publishing {

    repositories{
        maven {
            name = "GithubPackages"
            url = "https://maven.pkg.github.com/odk-x/androidlibrary"
            credentials {
                username = System.getenv('GITHUB_ACTOR')
                password = System.getenv('GITHUB_TOKEN')
            }
        }
    }
    publications {
        aarRelease(MavenPublication) {
            groupId = "org.opendatakit"
            artifactId = 'androidlibrary'
            version = "1.0.0-SNAPSHOT"
        }
        aarDebug(MavenPublication) {
            groupId = "org.opendatakit"
            artifactId = 'androidlibrary'
            version = "1.0.0-debug-SNAPSHOT"
        }
    }
}
